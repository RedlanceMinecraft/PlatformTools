plugins {
    id "maven-publish"
    id "java-library"
    id "java"
}

base.archivesName = "PlatformTools"
version = "0.0.1"
group = "org.redlance"

repositories {
    mavenCentral()
    maven {
        url = uri("https://jitpack.io")
    }
}

dependencies {
    compileOnlyApi("org.jetbrains:annotations:24.0.0")
    api("net.java.dev.jna:jna:5.17.0")

    api("com.github.dima-dencep:jfa:a141bbc812") {
        exclude(group: "net.java.dev.jna")
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set 17
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "platformtools"

            pom {
                name = base.archivesName
                url = "https://github.com/RedlanceMinecraft/PlatformTools"

                developers {
                    developer {
                        id = "dima_dencep"
                        name = "dima_dencep"
                        email = "dima_dencep@redlance.org"
                    }
                }

                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/RedlanceMinecraft/PlatformTools/blob/HEAD/LICENSE"
                    }
                }

                scm {
                    connection = "scm:git:github.com/RedlanceMinecraft/PlatformTools.git"
                    developerConnection = "scm:git:github.com/RedlanceMinecraft/PlatformTools.git"
                    url = "https://github.com/RedlanceMinecraft/PlatformTools.git"
                }
            }

            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}
