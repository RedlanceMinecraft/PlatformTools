plugins {
    id "xyz.wagyourtail.jvmdowngrader" version "1.3.6"
    id "maven-publish"
    id "java-library"
    id "java"
}

base.archivesName = "PlatformTools"
version = "3.2.0"
group = "org.redlance"

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.redlance.org/public")
    }
}

dependencies {
    compileOnlyApi("org.jetbrains:annotations:24.0.0")
    api("net.java.dev.jna:jna:5.18.1")

    // macOS
    implementation("ca.weblite:java-objc-bridge:1.2")
}

def javaTarget = 17

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(javaTarget)
}

jar {
    manifest.attributes("Multi-Release": true)
}

downgradeJar {
    dependsOn jar

    def versions = []
    for (int i = javaTarget; i >= 8; i--) {
        versions.add(JavaVersion.toVersion(i))
    }

    multiReleaseOriginal = true
    multiReleaseVersions = versions

    downgradeTo = JavaVersion.VERSION_1_8
    classpath = configurations.compileClasspath

    inputFile = tasks.jar.archiveFile
}

shadeDowngradedApi {
    downgradeTo = JavaVersion.VERSION_1_8
    shadePath {
        return "org/redlance/platformtools/impl/"
    }
    archiveClassifier.set("java8")
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadeDowngradedApi) {
                classifier = "java8"
                extension = "jar"
            }

            artifactId = "platformtools"

            pom {
                name = base.archivesName
                description = "Use platform-dependent code in java (via jna)"
                url = "https://github.com/RedlanceMinecraft/PlatformTools"

                developers {
                    developer {
                        id = "dima_dencep"
                        name = "dima_dencep"
                        email = "dima_dencep@redlance.org"
                    }
                }

                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/RedlanceMinecraft/PlatformTools/blob/HEAD/LICENSE"
                    }
                }

                scm {
                    connection = "scm:git:github.com/RedlanceMinecraft/PlatformTools.git"
                    developerConnection = "scm:git:github.com/RedlanceMinecraft/PlatformTools.git"
                    url = "https://github.com/RedlanceMinecraft/PlatformTools.git"
                }
            }

            from components.java
        }
    }

    repositories {
        maven {
            name = "RedlanceMinecraft"
            url = "https://repo.redlance.org/public"
            credentials {
                username = "dima_dencep"
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
